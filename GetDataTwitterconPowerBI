/*
Este script obtiene un token access y realiza una búsqueda de tweets desde la API REST de Twitter
https://dev.twitter.com/oauth/application-only

Se requiere la creacion de una aplicación Twitter para obtener una clave de consumidor y un secreto de consumidor
https://apps.twitter.com/

IMPORTANTE: El Consumer-Key y Consumer-Secret deben tratarse como contraseñas y no distribuirse
*/

let
 
 // Concatenamos el Consumer Key & Consumer Secret y luego lo transformamos a base64
 // Se debe reemplazar los valores de <ConsumerKey>:<ConsumerSecret>
 authKey = "Basic " & Binary.ToText(Text.ToBinary("<Aqui-pegar-ConsumerKey>:<Aqui-pegar-ConsumerSecret>"),0),
 url = "https://api.twitter.com/oauth2/token",
 
 // Usar el metodo Twitter POST oauth2/token para obtener el token de acceso
 GetJson = Web.Contents(url,
     [
         Headers = [#"Authorization"=authKey,
                    #"Content-Type"="application/x-www-form-urlencoded;charset=UTF-8"],
         Content = Text.ToBinary("grant_type=client_credentials") 
     ]
 ),
 FormatAsJson = Json.Document(GetJson),
 
 // Tomando el  token de respuesta desde Json
 AccessToken = FormatAsJson[access_token],
 AccessTokenHeader = "bearer " & AccessToken,
 
 // Realizando la busqueda en Twitter, la variable BUSQUEDA esta defina en su Panel de PowerBI
 GetJsonQuery = Web.Contents("https://api.twitter.com/1.1/search/tweets.json?q="&Busqueda&"&count=100",
     [
         Headers = [#"Authorization"=AccessTokenHeader]
     ]
 ),
 FormatAsJsonQuery = Json.Document(GetJsonQuery),
 NavigateToStatuses = FormatAsJsonQuery[statuses],
 TableFromList = Table.FromList(NavigateToStatuses, Splitter.SplitByNothing(), null, null, ExtraValues.Error),
 ExpandColumn = Table.ExpandRecordColumn(TableFromList, "Column1", {"metadata", "created_at", "id", "id_str", "text", "source", "truncated", "in_reply_to_status_id", "in_reply_to_status_id_str", "in_reply_to_user_id", "in_reply_to_user_id_str", "in_reply_to_screen_name", "user", "geo", "coordinates", "place", "contributors", "is_quote_status", "retweet_count", "favorite_count", "entities", "favorited", "retweeted", "lang", "possibly_sensitive", "quoted_status_id", "quoted_status_id_str", "quoted_status"}, {"Column1.metadata", "Column1.created_at", "Column1.id", "Column1.id_str", "Column1.text", "Column1.source", "Column1.truncated", "Column1.in_reply_to_status_id", "Column1.in_reply_to_status_id_str", "Column1.in_reply_to_user_id", "Column1.in_reply_to_user_id_str", "Column1.in_reply_to_screen_name", "Column1.user", "Column1.geo", "Column1.coordinates", "Column1.place", "Column1.contributors", "Column1.is_quote_status", "Column1.retweet_count", "Column1.favorite_count", "Column1.entities", "Column1.favorited", "Column1.retweeted", "Column1.lang", "Column1.possibly_sensitive", "Column1.quoted_status_id", "Column1.quoted_status_id_str", "Column1.quoted_status"})
in
 ExpandColumn
// Fuente: http://chris.koester.io/index.php/2015/07/16/get-data-from-twitter-api-with-power-query/ 
